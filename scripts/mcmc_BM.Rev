################################################################################
#
# Chris Deardeuff -- edited from
# RevBayes Example: Bayesian inference of rates of evolution under a
#                   constant-rate Brownian-motion model
#
#
# Original authors: Michael R. May and Sebastian HÃ¶hna
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the trait to analyze
trait <- 1

### Read in the trees
T <- readTrees("../data/penguin_mcc.tre")[1]

### Read in the character data
data <- readContinuousCharacterData("../data/penguin_log_bodymass.nex")
data.excludeAll()
data.includeCharacter( trait )

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

##########################
# Specify the tree model #
##########################

tree <- T

##########################
# Specify the rate model #
##########################

sigma2 ~ dnLoguniform(1e-5, 1e-1)
moves.append( mvScale(sigma2, weight=1.0) )


##########################
# Specify the BM process #
##########################

X ~ dnPhyloBrownianREML(tree, branchRates=sqrt(sigma2) )
X.clamp(data)



#############
# The Model #
#############

mymodel = model(sigma2)

### set up the monitors that will output parameter values to file and screen
monitors.append( mnModel(filename="../output/simple_BM.log", printgen=10) )
monitors.append( mnScreen(printgen=1000, sigma2) )

################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")


### run the MCMC ###
mymcmc.burnin(generations=5000, tuningInterval=100)
mymcmc.run(generations=50000, tuningInterval=100)

## quit ##
q()

